WHITESPACE = _{" " | "\t" | "\n"}

short_literal = @{"$"~ASCII_HEX_DIGIT{2}}
long_literal = @{"$"~ASCII_HEX_DIGIT{4}}
number = {long_literal | short_literal}
instruction = {
    ^"LDA" | ^"LDX" | ^"LDY" | ^"STA" | ^"STX" | ^"STY" | ^"ADC" | ^"SBC" | ^"INC" | ^"INX" | ^"INY" | ^"DEC" | ^"DEX" | ^"DEY" |
    ^"AND" | ^"ORA" | ^"EOR" | ^"JMP" | ^"BCC" | ^"BCS" | ^"BEQ" | ^"BNE" | ^"BMI" | ^"BPL" | ^"BVS" | ^"BVC" | ^"CMP" | ^"CPX" |
    ^"CPY" | ^"BIT" | ^"ASL" | ^"LSR" | ^"ROL" | ^"ROR" | ^"TSX" | ^"TXS" | ^"PHA" | ^"PHP" | ^"PLA" | ^"PLP" | ^"JSR" | ^"RTS" |
    ^"RTI" | ^"CLC" | ^"CLD" | ^"CLI" | ^"CLV" | ^"SEC" | ^"SED" | ^"SEI" | ^"NOP" | ^"BRK"
}

label = @{!instruction~(ASCII_ALPHA~(ASCII_ALPHANUMERIC | "_")*)}
label_dec = ${label~":"}
address = {label | number}

indexed_indirect_addresser = ${"("~address~","~"X"~")"}
indirect_indexed_addresser = ${"("~address~")"~","~"Y"}
indirect_addresser = ${"("~address~")"}
direct_addresser = ${"#"~address}
indexed_addresser = ${address~","~("X" | "Y")}

address_code = _{
    direct_addresser           |
    indexed_indirect_addresser |
    indirect_indexed_addresser |
    indirect_addresser         |
    indexed_addresser          |
    address
}

operation = _{instruction~(!label_dec~address_code)?}
labeled_operation = _{label_dec+~operation}
expression = {operation | labeled_operation}

program = {SOI~(expression)*~EOI}
